Multichar_Symbols

+N +Po
+Abs +Dat +Ins +Soc +Erg +Ine +Ben +Ben2 +Par +All +Ela +Term +Ten
+Art
+Locp
+Ben2
+PGen
+LGen
+Mgg
+XXX
+Prop

+Dim
+Pl
+Sg

+Pron

+V
+Aux
+ifl
+ifl/f
+ifl/m
+Ind
+Prs
+Prt
+Abs/Sg1
+Abs/Sg2
+Abs/Sg3
+Abs/Pl1
+Abs/Pl2
+Abs/Pl3
+Dat/Sg1
+Dat/Sg2
+Dat/Sg3
+Dat/Pl1
+Dat/Pl2
+Dat/Pl3

%>

LEXICON Root

Nouns ;
Propernouns ;
Pronouns ;
Verbs ;

LEXICON SgCases1 

+Sg+Abs: # ; !-ak
+Sg+Erg:%>k # ; !-ek
+Sg+Dat:%>ri # ; !-ei

+Sg+PGen:%>ren # ; !-en
+Sg+Ins:%>z # ; !-ez, taz, etaz
+Sg+Soc:%>rekin # ; !-ekin
+Sg+Ben:%>rentzat # ; !-entzat
+Sg+Ben2:%>rengatik # ; !-agatik !-engatik

LEXICON SgCases2

+Sg+Ine: # ; !-ean !-etan
!twolc rule - larunbata - larunbatean // isn't the lemma 'larunbat' here ? 
+Sg+All:%>ra # ; !-era !-etara
+Sg+Ela:%>tik # ; !-etik !-etatik

LEXICON SgCases3

+Sg+LGen:%>ko # ; !-eko !-etako


LEXICON PlCases1 

+Pl+Abs:%>k # ;
+Pl+Erg:%>ek # ;
+Pl+Dat:%>ei # ;

+Pl+PGen:%>en # ; 
+Pl+Ins:%>ez # ; 
+Pl+Soc:%>ekin # ;
+Pl+Ben:%>entzat # ;
+Pl+Ben2:%>engatik # ;

LEXICON PlCases2 

+Pl+Ine: # ;
+Pl+All:%>etara # ;
+Pl+Ela:%>etatik # ;

LEXICON PlCases3 

+Pl+LGen:%>etako # ;


LEXICON MggCases1 

+Abs: # ;
+Erg:%>k # ;   !k
+Dat:%>ri # ;   !i

+PGen:%>ren # ;    !en
+Ins:%>z # ;       !ez,etaz,taz
+Soc:%>rekin # ;   !ekin
+Ben:%>rentzat # ; !entzat
+Ben2:%>rengatik # ;   !engatik

+Par:%>rik # ;

LEXICON MggCases2

+Ine:%>etan # ;     !tan
+All:%>etara # ;    !tara
+Ela:%>etatik # ;   !tatik


LEXICON MggCases3

+LGen:%>etako # ;   !tako



LEXICON Propcases 

+Abs: # ; 
+Erg:%>k # ; ! -ek
+Dat:%>i # ; ! -ri

+Ine:%>n # ; ! -en
+All:%>ra # ; ! -a
+Ela:%>tik # ; !-dik
+LGen:%>ko # ; !-go

+PGen:%>ren # ; !-en
+Ins:%>z # ; !-ez, taz, etaz
+Soc:%>rekin # ; !-ekin
+Ben:%>rentzat # ; !-entzat
+Ben2:%>rengatik # ; !-engatik


LEXICON Bizidun !after animate noun phrases

+Biz: SgCases1 ;
+Biz: PlCases1 ;
+Biz: MggCases1 ;
+Biz:%>gan SgCases2 ;
+Biz:%>rengan PlCases2 ;
+Biz:%>rengan MggCases2 ;

!LEXICON Loccases
!
!+Ela:%>dik # ; !"from"
!+All:%>a # ; ! "to"
!+Term:%>aino # ; !"as far as, up to"
!+Ten:%>antz # ; ! "toward"
!
!LEXICON Precases !nouns that can denote a location, they can take another case in addition
!
!+Sg+Term:%>raino # ; !Terminal "as far as"
!+Sg+Gen:%>ko # ;
!+Sg+XXX:%>koa # ;
!+Sg+Ten:%>rantz # ; !Tendential "toward"


!for propernouns ending in a consonant

!LEXICON Dim
!+Dim:%>txo Cases ; !tto, no

LEXICON Nreg

+N: MggCases1 ;
+N: MggCases2 ;
+N: MggCases3 ;
+N+Art:%>a SgCases1 ;
+N+Art:%>a SgCases2 ;
+N+Art:%>a SgCases3 ;
+N+Art:%>a PlCases1 ;
+N+Art:%>a PlCases2 ;
+N+Art:%>a PlCases3 ;
!+N: Dim ;

LEXICON NregBizi

+N: Bizidun ;
+N+Art:%>a Bizidun ;
!+N: Dim ;

LEXICON Na

+N: MggCases1 ;
+N: MggCases2 ;
+N: MggCases3 ;
+N+Art: SgCases1 ;
+N+Art: SgCases2 ;
+N+Art: SgCases3 ;
+N+Art: PlCases1 ;
+N+Art: PlCases2 ;
+N+Art: PlCases3 ;
!+N: Dim ;

LEXICON NaBizi

+N: Bizidun ;
+N+Art: Bizidun ;
!+N: Dim ;

LEXICON Np

+N+Prop: Propcases ;


LEXICON Aux

+V+Aux: IZANNOR ;

LEXICON IZANNOR

+Ind+Prs+Abs/Sg1:naiz # ;
+Ind+Prs+Abs/Sg2+ifl:haiz # ; !+Ind+Prs+Abs/hi
+Ind+Prs+Abs/Sg2:zara # ;
+Ind+Prs+Abs/Sg3:da # ;
+Ind+Prs+Abs/Pl1:gara # ;
+Ind+Prs+Abs/Pl2:zarete # ;
+Ind+Prs+Abs/Pl3:dira # ;

!prt - en
+Ind+Prt+Abs/Sg1:nintzen # ;
+Ind+Prt+Abs/Sg2+ifl:hintzen # ;
+Ind+Prt+Abs/Sg2:zinen # ;
+Ind+Prt+Abs/Sg3:zen # ;
+Ind+Prt+Abs/Pl1:ginen # ;
+Ind+Prt+Abs/Pl2:zineten # ;
+Ind+Prt+Abs/Pl3:ziren # ;

LEXICON IZANNORNORI

+Ind+Abs/Sg1:nintzai IZANNORI ;
+Ind+Abs/Sg2+ifl:hintzai IZANNORI ; !+Ind+Prs+Abs/hi
+Ind+Abs/Sg2:zintzai IZANNORI ;
+Ind+Abs/Sg3:zitzai IZANNORI ;
+Ind+Abs/Pl1:gintzaizki IZANNORI ;
+Ind+Abs/Pl2:zintzaizki IZANNORI ;
+Ind+Abs/Pl3:aitzaizki IZANNORI ;

LEXICON IZANNORI

+Dat/Sg1:%>t       PAST ; !dan before a consonant
+Dat/Sg2+ifl/m:%>k PAST ; !kian before a consonant
+Dat/Sg2+ifl/f:%>n PAST ; !
+Dat/Sg2:%>zu      PAST ;
+Dat/Sg3:%>o       PAST ;
+Dat/Pl1:%>gu      PAST ;
+Dat/Pl2:%>zuete   PAST ;
+Dat/Pl3:%>e       PAST ;

LEXICON PAST

+Prt:n # ;



LEXICON Nouns

etxe Nreg "house" ;
pisu Nreg "flat" ;
taberna Na "bar" ;
txakur NregBizi "dog" ;
neska NaBizi "girl" ;
seme NregBizi "son" ;
mendi Nreg "mountain" ;

LEXICON Propernouns

Frantzia Np "France" ;
Galizia Np "Galicia" ;
Grezia Np "Greece" ;
Japonia Np "Japan" ;
Katalunia Np "Catalonia" ;
Polonia Np "Poland" ;
Suedia Np "Sweden" ;
Suitza Np "Switzerland" ;
Turkia Np "Turkey" ;
Txekia Np "Czech Republic" ;
Baiona Np "Bayonne" ;
Gipuzkoa Np "Guipuzcoa" ;
Aezkoa Np "Aezcoa" ;
Baiona Np "Bayonne" ;
Hendaia Np "Hendaye" ;
Bidasoa Np "Bidasoa" ; 
Leioa Np "Lejona" ;
Donostia Np "San Sebastian" ;
Nafarroa Np "Navarra" ;
Errenteria Np "Renteria" ;
Ordizia Np "Villafranca de Oria" ;
Getaria Np "Guetaria" ;
Zuberoa Np "Soule" ;

LEXICON Verbs

asmatu Mv "invent" ;
izan Aux "be" ;
ukan Aux "be" ;

