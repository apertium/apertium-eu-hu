


!##############################################################################

Multichar_Symbols

+N +Po +Det +Pron +CC +CS
+Nom +Acc +Dat +Ins +Caus +Tra +Term +Ess1 +Ess2 +Ine +SEss +Ade +Ill +Sub +All +Ela +Del +Abl +Gen +Form +Dist +Soc +Loc +DTemp +Temp
+Pl 
+Sg1 +Sg2 +Sg3 +Pl1 +Pl2 +Pl3
+PxSg +PxPl
+PxSg1 +PxSg2 +PxSg3 +PxPl1 +PxPl2 +PxPl3
+Dem +Pers +Recip +Rel +Def +Indef
+Use/LR +Use/RL

%^v  ! any vow
%^U  ! u or y
%^A  ! a or e
%^Ó  ! ó or ő
%^Á  ! á or é
%^EÖ ! o or e or ö
%^AEUY ! one of these -- is this just the wrong group? -- no 'y' in hungarian
%^SHORT ! demand short vowel before -kor / -ként
%^ASSIM ! initial -v- of suffix assimilates to the stem final consonant

!##############################################################################

LEXICON Root

Nouns ;
Pronouns ;
Determiners ;
Conjunctions ;

!##############################################################################
! 
! 0      I      II       III          IV                 V
! Stem + Num. + Poss. + (Nom. poss. + Nom. poss. num.) + Case
! ház                                                            'ház'
! ház    ak                                                      'házak'
! ház    ak              é            i                  ban     'házakéiban'
! ház           am
! ház           am       é                                
! ház           am       é                               ban     'házaméban' 
! ház           am       é            i
! ház    ai     m        
!
!##############################################################################

LEXICON Cases 

+Nom: # ;                    !
+Acc:%>t #  ;                ! -t
+Ill:%>b%^A #  ;             ! -ba/-be
+Ine:%>b%^An #  ;            ! -ban/-ben
+Ela:%>b%^Ól #  ;            ! -ból/-ből
+Sub:%>r%^A #  ;             ! -ra/-re
+SEss:%>%^EÖn #  ;           ! -on/-en/-ön
+Del:%>r%^Ól #  ;            ! -ról/-ről
+All:%>h%^EÖz #  ;           ! -hoz/-hez/-höz 
+Ade:%>n%^Ál #  ;            ! -nál/-nél
+Abl:%>t%^Ól #  ;            ! -tól/-től
+Dat:%>n%^Ak #  ;            ! -nak/-nek
+Ins:%>%^ASSIMv%^Al #  ;            ! -val/-vel
+Tra:%>%^ASSIMv%^Á #  ;             ! -vá/-vé
+Caus:%>ért # ;              ! -ért
+Ess1:%>%^SHORTként # ;      ! -ként 
+Term:%>ig # ;               ! -ig
+Dist:%>{^oa}nként # ;       ! -onként/-anként/-enként/-önként
+Temp:%>%^SHORTkor # ;       ! -kor
+DTemp:%>%^EÖnta # ;         ! -onta/-enta/-önta
+Soc:%>{^oa}stul # ;         ! ?

LEXICON Poss

Cases ; 
+PxSg:é Cases ;
+PxPl:éi Cases ; 

LEXICON PossAttr

Poss ;
PossSuff ;

LEXICON PossSuff
+PxSg1:%>m Poss ;
+PxSg2:%>d Poss ;
+PxSg3:%>ja Poss ;
+PxPl1:%>nk Poss ;
+PxPl2:%>tok Poss ;
+PxPl3:%>juk Poss ;

!##############################################################################

LEXICON Nreg

+N: PossAttr ;
+N+Pl:%>k Cases ;
+N+Pl:%>i PossSuff ;

!##############################################################################

LEXICON CC

+CC: # ;

LEXICON CS

+CS: # ;

LEXICON Conjunctions 

és CC "and";
meg CC "and";
de CC "but";
vagy CC "or";

!##############################################################################

LEXICON Determiners 

a+Det+Def+Use/RL:%~a # ;
a+Det+Def+Use/LR:a # ;
a+Det+Def+Use/LR:az # ;

egy+Det+Indef:egy # ;


!##############################################################################

LEXICON Pronouns

én+Pron+Pers+Sg1:én Cases ;

!##############################################################################

LEXICON Nouns

lakás Nreg  "flat" ;
lámpa Nreg  "lamp" ;
táska Nreg  "bag" ;
csésze Nreg  "cup" ;
lecke Nreg  "lesson" ;

víz Nreg "water" ;
bor Nreg "wine" ;
ész Nreg "mind" ;
asztal Nreg "table" ;
toll Nreg "pen" ;
orr Nreg "nose" ;
lány Nreg "girl" ;
nő Nreg  "woman" ;
tű Nreg  "needle" ;
kocsi Nreg  "car" ;
hajó Nreg  "boat" ;
kapu Nreg  "gate" ;
barát Nreg  "friend" ;

ház Nreg   "house"  ;
