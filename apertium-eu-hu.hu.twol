Alphabet
 a á b c d e é f g h i í j k l m n o ó ö ő p q r s t u ú ü ű v w x y z
 A Á B C D E É F G H I Í J K L M N O Ó Ö Ő P Q R S T U Ú Ü Ű V W X Y Z 

 %>:0 %^SHORT:0 %^ASSIM:0
 %^v:0 %^U:y %^A:e %^OAEÖ:e %^Ó:ő %^Á:é %^EÖ:e 
 ;

!%^SHORT for avoiding a/e lengthening in front of -kor etc.
!%^ASSIM for -v- consonant assimilation

Sets

Cns = b c d f g h j k l m n p q r s t v w x z
      B C D F G H J K L M N P Q R S T V W X Z ;
FrontVow = e é i í ü ű ö ő ;
FrontRoundedVow = ö ő ü ű ;
FrontUnroundedVow = e é i í;
BackRoundedVow = o ó u ú ;
BackUnroundedVow = a á ; 
BackVow = a á o ó u ú ;
ArchiVow =  %^v %^U %^A %^OAEÖ %^Ó %^Á %^EÖ ;
Vow = FrontVow BackVow ArchiVow ;
Segment = FrontVow BackVow ArchiVow Cns ;
NonFront = Cns  BackVow ArchiVow %> ;
NonBack =  Cns FrontVow ArchiVow %> ;
NonBackRounded = Cns FrontVow BackUnroundedVow ArchiVow ;
NonFrontUnrounded = Cns BackVow ;

! consider adding the archivowels to the vowel sets, look at that.

Rules

! Vowel harmony rules in Hungarian require that front or back assonance
! in the vowels of a stem be maintained through the entire word, thus 
! for the most part -- except some recent loans -- Hungarian words have
! either only back or only front vowels.
!
! Because vowel assonance is maintained throughout the whole word, most 
! suffixes have front and back vowel variants, e.g. the dative -nak (B)
! -nek (F). Thus if a stem contains back vowels it affixes back vowel suffixes
! should the word contain only front vowels, it gets front vowel suffixes.
! 
! When a suffix contains only front vowels, then a second distinction is 
! made: rounded/unrounded in the _last_ vowel. If the stem's last vowel 
! is front and rounded it takes a suffix with a front rounded vowel. If 
! the stem's _last_ vowel is front and unrounded it take s front unrounded
! suffix. Although suffixes for most words have front/back variants, only 
! a few endings have unrounded/rounded variants (e.g. allative -hoz/-hez/-höz)
! and the plural suffix (-k/-ok/-ek/-ök)
!
! Front unrounded vowels are also considered 'neutral'; a word containing
! back vowels and front unrounded vowels is considered a back vowel word. If
! _only_ front unrounded vowels appear in a stem, then it is considered to be
! of front vowel assonance. Although there are exceptions (e.g. szív 'heart' 
! has front vowel assonance, but szív 'inhale' has back vowel assonance).

!  +SEss:%>%^EÖn #  ;           ! -(o)n/-(e)n/-(ö)n

"Short Back EÖ Harmony"
%^EÖ:o <=> BackVow: NonFront:* %>: NonFront:* _ ;

!€ház>^EÖn
!€ház>00on

"Short Front Round Harmony"
%^EÖ:ö <=> FrontRoundedVow: NonFront:* %>: NonFront:* _ ;

!€könyv>^EÖn
!€könyv>00ön

"Short Back harmony"
%^A:a <=> BackVow: NonFront:* %>: NonFront:* _ ;

!ház>b^An
!ház>b0an
  
!ész>b^An
!ész>b0en

"Long Back harmony"
%^Á:á <=> BackVow: NonFront:* %>: NonFront:* _ ;

"Vowel lengthening after stem-final a followed by suffix"
a:á <=> _ %>: Segment: ;

!€lámpa>nak
!€lámpá>nak

!€lámpa>^EÖn
!€lámpá>^EÖn

"Vowel lengthening after stem-final e followed by suffix"
e:é <=> _ %>: Segment: ;

!€csésze>nek
!€csészé>nek
  
"Vowel shorting after vowel-final stems"
 %^EÖ:0 <=> Vow %>: _ ;

"Digraph consonant assimilation with -v- suffixes" 
Cb:Ca <=> Cx _ %>: %^ASSIM:0 v:Cy %^A: ;
          where Ca in (c d g n s t z)
                Cb in (s z y y z y s) 
                Cx in (c d g n s t z)
                Cy in (s z y y z y s) matched ;

!€ész>^ASSIMv^Al
!€éss>000000z^Al

!€lány>^ASSIMv^Al
!€lánn>000000y^Al

!
! barátok + val -> barátokkal
! barátom + val -> barátommal
!
!"Consonant assimilation with -v- suffixes" 
!v:Cx <=> Cy %>: %^ASSIM:0 _  ;
!          where Cx in (b f h j k l m p q r v w x) 
!                Cy in (b f h j k l m p q r v w x) matched ;
!
!€ház>^ASSIMv^Al
!€ház>000000z^Al
